000010 IDENTIFICATION   DIVISION.
000020 PROGRAM-ID.            JK072U.
000030******************************************************************
000040*    出荷実績データ送信                                          *
000050*    DATE    :     90/04/26                                      *
000060*    AUTHOR  :     IKUMI.N                                       *
000070******************************************************************
000080 ENVIRONMENT            DIVISION.
000090 CONFIGURATION          SECTION.
000100 SOURCE-COMPUTER.       SYSTEM3100.
000110 OBJECT-COMPUTER.       SYSTEM3100.
000120 INPUT-OUTPUT           SECTION.
000130 FILE-CONTROL.
000140*----Ｏ／Ｌ送信Ｆ
000150     SELECT   KOLJF     ASSIGN  F1-MSD
000160                        ORGANIZATION  SEQUENTIAL
000170                        ACCESS        SEQUENTIAL
000180                        FILE  STATUS  ERR-STAT.
000190*----Ｏ／Ｌ状況Ｆ
000200     SELECT   JOJF      ASSIGN  F2-MSD
000210                        ORGANIZATION  INDEXED
000220                        ACCESS        DYNAMIC
000230                        RECORD        JOJF-KEY
000240                        FILE  STATUS  ERR-STAT.
000250 I-O-CONTROL.
000260     APPLY    SHARED-MODE  ON  JOJF.
000270 DATA                DIVISION.
000280 FILE                SECTION.
000290 COPY    L-KOJF.
000300 COPY    L-JOJF.
000310 WORKING-STORAGE  SECTION.
000320 01  ERR-STAT                PIC  X(02).
000330 01  DSEC                    PIC  9(04)    VALUE 10.
000340 01  WCSSD                   PIC  X(12)    VALUE  SPACE.
000350 01  WCSD                    PIC  X(20)    VALUE  SPACE.
000360 01  SENDBUF                 PIC  X(256).
000370 01  RECVBUF                 PIC  X(256).
000380 01  RECVEOT                 PIC  X(256).
000390 01  WORK-ARIA.
000400     02  MM                  PIC  9(02).
000410     02  NN                  PIC  9(02).
000420     02  NA                  PIC  9(02).
000430     02  NB                  PIC  9(02).
000440     02  SW                  PIC  X(01).
000450     02  W-PF9               PIC  X(01).
000460     02  STR-TIME            PIC  9(08).
000470     02  STR-TIMER           REDEFINES  STR-TIME.
000480         03  STR-JF          PIC  9(04).
000490         03  F               PIC  X(04).
000500     02  END-TIME            PIC  9(08).
000510     02  END-TIMER           REDEFINES  END-TIME.
000520         03  END-JF          PIC  9(04).
000530         03  F               PIC  X(04).
000540     02  JOJF-RECW           PIC  X(512).
000550     02  TAMEKOMI-WORK.
000560         03  STR-CODE        PIC  X(10).
000570         03  END-CODE        PIC  X(10).
000580         03  MSG-N           PIC  X(22).
000590         03  JOJF-08W        PIC  9(02).
000600     02  W-JCD               PIC  9(03).
000610     02  W-ACD               PIC  9(03).
000620***************　制御電文　*********************
000630 01  F1-REC.
000640     03  S-ID                PIC  X(02).
000650     03  S-JECD              PIC  9(03).                          自営ｺｰﾄﾞ
000660     03  S-AECD              PIC  9(03).                          相手先CD
000670     03  S-SKSU              PIC  9(06).                          送信件数
000680     03  S-KUB               PIC  X(02).                          処理区分
000690     03  FILLER              PIC  X(48).
000700*
000710 01  W-DATE.
000720     02  W-YY                PIC  9(02).
000730     02  W-MD                PIC  9(04).
000740     02  W-MDR               REDEFINES  W-MD.
000750         03  W-MM            PIC  9(02).
000760         03  W-DD            PIC  9(02).
000770 01  W-TIME.
000780     02  WWTIME              PIC  9(06).
000790*----SCREEN WORK
000800 01  SCREEN-ARIA.
000810     02  KAKU-W              PIC  X(01).                          確認
000820     02  KENSU               PIC  9(06).                          件数
000830     02  SOUKEN              PIC  9(06).                          送信件数
000840     02  MSG-W2              PIC  N(20).
000850*
000860     02  W-JKYO              PIC  N(04).                          通信状況
000870     02  W-EMGA.                                                  エラー
000880         03  W-EMGA1         PIC  X(02).
000890         03  W-EMGA2         PIC  X(01).
000900     02  W-EMGN              PIC  N(10).
000910     02  OKC                 PIC  X(01).
000920 01  SW-AREA.
000930     02  END-SW                  PIC  9(01).
000940     02  INV-SW                  PIC  9(01).
000950     02  ERR-SW                  PIC  9(01).
000960 01  WCTL6-R.
000970     02  WCTL6-01                PIC  9(01).
000980     02  WCTL6-02                PIC  9(01).
000990     02  WCTL6-03                PIC  9(02).
001000     02  FILLER                  PIC  X(252).
001010 01  MES-AREA.
001020     02  INV-NAM       PIC N(09) VALUE  NC"名称マスター未登録".
001030     02  INV-OLC       PIC N(08) VALUE  NC"伝送制御Ｆ未登録".
001040     02  MSG-CW.
001050         03  MC01      PIC N(10) VALUE  NC"この相手先は送信済".
001060         03  MC02      PIC N(10) VALUE  NC"相手先エラー".
001070         03  MC03      PIC N(10) VALUE  NC"送信データ無し".
001080         03  MC04      PIC N(10) VALUE  NC"送受信件数アンマッチ".
001090     02  SEND-MES.
001100         03  SM-A      PIC N(10) VALUE  NC"回線不良　（Ｓ）".
001110         03  SM-B      PIC N(10) VALUE  NC"送信要求拒否".
001120         03  SM-C      PIC N(10) VALUE  NC"回線断　（Ｓ）".
001130         03  SM-E      PIC N(10) VALUE  NC"ＮＣＵ使用不能".
001140         03  SM-G      PIC N(10) VALUE  NC"タイムアウト".
001150         03  SM-J      PIC N(10) VALUE  NC"相手局は話し中".
001160         03  SM-ER     PIC N(10) VALUE  NC"送信エラー".
001170     02  RECV-MES.
001180         03  RM-L      PIC N(10) VALUE  NC"回線不良　（Ｒ）".
001190         03  RM-R      PIC N(10) VALUE  NC"ＮＣＵ使用不能".
001200         03  RM-S      PIC N(10) VALUE  NC"タイムアウト".
001210         03  RM-T      PIC N(10) VALUE  NC"相手局は話し中".
001220         03  RM-ER     PIC N(10) VALUE  NC"受信エラー".
001230     02  ENDI-MES.
001240         03  ENDI-16   PIC N(10) VALUE  NC"該当回線は他で使用中".
001250         03  ENDI-20   PIC N(10) VALUE  NC"通信環境が未設定".
001260         03  ENDI-ER   PIC N(10) VALUE  NC"回線オープンエラー".
001270     02  MSG-AW.
001280         03  MA01      PIC N(04) VALUE  NC"送信中".
001290         03  MA02      PIC N(04) VALUE  NC"送信異常".
001300         03  MA03      PIC N(04) VALUE  NC"正常終了".
001310*
001320     02  MSG-WORK.
001330         03  F         PIC N(07) VALUE  NC"送信データ無し".
001340         03  F         PIC N(07) VALUE  NC"送　　信　　中".
001350         03  F         PIC N(07) VALUE  NC"正常　終了　　".
001360         03  F         PIC N(07) VALUE  NC"通信不可　　　".
001370     02  MSG-WORKR     REDEFINES  MSG-WORK.
001380         03  MSG-M     OCCURS  4  PIC  N(07).
001390***
001400 COPY    LWMSG.
001410***
001420 COMMUNICATION        SECTION.
001430 CD  RECVREC          FOR   INPUT
001440     SYMBOLIC         QUEUE         IS   CRSQ
001450     SYMBOLIC         SOURCE        IS   CRSS
001460     TEXT             LENGTH        IS   CRTL
001470     END              KEY           IS   CREK
001480     STATUS           KEY           IS   CRSK
001490     MARK                           IS   CRM
001500     DIAL                           IS   CRD
001510     UNIT             SELECTION     IS   CRUS.
001520 CD  SENDREC          FOR   OUTPUT
001530     TEXT             LENGTH        IS   CSTL
001540     STATUS           KEY           IS   CSSK
001550     ERROR            KEY           IS   CSEK
001560     SYMBOLIC         DESTINATION   IS   CSSD
001570     MARK                           IS   CSM
001580     DIAL                           IS   CSD
001590     DEVICE           MARK          IS   CSDM.
001600*-----------------------------------------------------------------*
001610*----            << SCREEN  SECTION >>                        ----*
001620*-----------------------------------------------------------------*
001630 SCREEN             SECTION.
001640 SD  CRT-X
001650     END STATUS     END-STS.
001660*
001670 01  DSP-AREA.
001680     02  LINE  01   COLUMN  20
001690               VALUE  NC"＊　出荷実績データ送信　＊".
001700 01  DSP-AREA2.
001710     02  LINE  07   COLUMN  20  VALUE  NC"出荷実績送信中".
001720     02  LINE  07   COLUMN  36  VALUE  NC"件数＝".
001730     02  LINE  07   COLUMN  51  VALUE  NC"件".
001740     02  LINE  09   COLUMN  36  VALUE  NC"送信＝".
001750     02  LINE  09   COLUMN  51  VALUE  NC"件".
001760*----データ件数表示
001770     02  DSP-KB01.
001780         03  LINE  07   COLUMN  43  PIC  ZZZ,ZZ9  FROM  KENSU.
001790     02  DSP-KB02   LINE 09.
001800         03  COLUMN  43  PIC  ZZZ,ZZ9      FROM   SOUKEN.
001810     02  DSP-MSG    LINE 23.
001820         03  DSP-MSG1 COLUMN 10 PIC  N(20) FROM  MSG-M(MM).
001830         03  COLUMN  41  PIC  X(03)        VALUE ""27"B"03"".
001840         03  COLUMN  10  PIC  X(40)        VALUE " ".
001850 01  CLR-AREA.
001860     02  LINE   07  COLUMN  43  PIC  X(07)  VALUE  " ".
001870     02  LINE   09  COLUMN  43  PIC  X(07)  VALUE  " ".
001880     02  DSP-04  LINE  23  COLUMN  1   PIC  N(04)
001890                 FROM  W-JKYO.
001900     02  DSP-05  LINE  23.
001910         03  DSP-051  COLUMN  10  PIC  X(03)  FROM  W-EMGA.
001920         03  DSP-052  COLUMN  14  PIC  N(10)  FROM  W-EMGN.
001930     02  DSP-CLER     LINE  23  COLUMN  1   PIC  X(80) VALUE  " ".
001940*
001950***
001960 COPY    LSERR.                                                   R.900411
001970***
001980 PROCEDURE        DIVISION.
001990******************************************************************
002000*    ＭＡＩＮ　ＲＯＵＴＩＮＥ　　　　　　                        *
002010******************************************************************
002020 MAIN.
002030     PERFORM  INI-RTN        THRU  INI-EX.
002040     IF  ERR-SW  =  1                  GO  TO  MR999.
002050**
002060     PERFORM  LOP-RTN        THRU  LOP-EX.
002070     IF  ERR-SW  =  1                  GO  TO  MR999.
002080**
002090     PERFORM  OPN-RTN        THRU  OPN-EX.
002100     PERFORM  DAM-RTN        THRU  DAM-EX.
002110     IF  ERR-SW  =  1                  GO  TO  MR888.
002120**
002130     PERFORM  SSD-RTN        THRU  SSD-EX.
002140     IF  ERR-SW  =  1                  GO  TO  MR888.
002150**
002160     PERFORM  ANS-RTN        THRU  ANS-EX.
002170     IF  ERR-SW  =  1                  GO  TO  MR888.
002180**
002190     PERFORM  MSG-RTN        THRU  MSG-EX.
002200     IF  ERR-SW  =  1                  GO  TO  MR888.
002210**
002220     PERFORM  ESD-RTN        THRU  ESD-EX.
002230     IF  ERR-SW  =  1                  GO  TO  MR888.
002240**
002250     PERFORM  ANE-RTN        THRU  ANE-EX.
002260     IF  ERR-SW  =  1                  GO  TO  MR888.
002270**
002280     PERFORM  UPD-RTN        THRU  UPD-EX.
002290 MR888.
002300     PERFORM  CLO-RTN        THRU  CLO-EX.
002310     PERFORM  END-RTN        THRU  END-EX.
002320 MR999.
002330     STOP  RUN.
002340******************************************************************
002350*    ＯＰＮ−ＲＴＮ　　（ＯＰＥＮ　処理）            　
002360******************************************************************
002370 OPN-RTN.
002380     OPEN     INPUT     KOLJF
002390              I-O       JOJF.
002400 OPN-EX.
002410     EXIT.
002420******************************************************************
002430*    ＩＮＩ−ＲＴＮ　　（初期＆画面処理）            　
002440******************************************************************
002450 INI-RTN.
002460     ACCEPT      CRSQ.
002470     ACCEPT      WCSSD.
002480     ACCEPT      W-JCD.
002490*
002500     DISPLAY     DSP-AREA  WITH    BYPASS.
002510*
002520 INI-EX.
002530     EXIT.
002540******************************************************************
002550*    ＬＯＰ−ＲＴＮ　　（開局）                                  *
002560******************************************************************
002570 LOP-RTN.
002580     MOVE   WCSSD       TO   CSSD.
002590     ENABLE OUTPUT SENDREC.
002600     PERFORM  ENDI-STSS-RTN  THRU  ENDI-STSS-EX.
002610     IF  ERR-SW  =  1                  GO  TO  LOP-EX.
002620*
002630     MOVE   WCSSD            TO   CRSS.
002640     ENABLE INPUT  RECVREC.
002650     PERFORM  ENDI-STSR-RTN  THRU  ENDI-STSR-EX.
002660     IF  ERR-SW  =  1
002670         PERFORM  CLO1-RTN         THRU  CLO1-EX
002680         MOVE   1            TO   ERR-SW.
002690 LOP-EX.
002700     EXIT.
002710******************************************************************
002720*    ＤＡＭ−ＲＴＮ　　（ダミー受信）                            *
002730******************************************************************
002740 DAM-RTN.
002750     RECEIVE   RECVREC   MESSAGE  INTO  RECVBUF  NO  DATA
002760              CALL     "CBLTIMER"  USING  DSEC
002770              GO  TO  DAM-RTN.
002780     PERFORM  RECV-STS-RTN  THRU  RECV-STS-EX.
002790     IF  ERR-SW  =  1                  GO  TO  DAM-EX.
002800 DAM-010.
002810     RECEIVE   RECVREC   MESSAGE  INTO  RECVEOT.
002820     PERFORM  RECV-STS-RTN  THRU  RECV-STS-EX.
002830     IF  ERR-SW  =  1                  GO  TO  DAM-EX.
002840     IF  CREK    NOT =  3              GO  TO  DAM-010.
002850     MOVE   RECVBUF     TO   F1-REC.
002860     IF  S-ID    NOT =  "ST"           GO  TO  DAM-RTN.
002870 DAM-EX.
002880     EXIT.
002890******************************************************************
002900*    ＳＳＤ−ＲＴＮ　　（開始要求電文　送信）                    *
002910******************************************************************
002920 SSD-RTN.
002930     ACCEPT  STR-TIME   FROM  TIME.
002940     ACCEPT  W-DATE     FROM  DATE.
002950     MOVE  0001     TO  JOJF-01.
002960*----ｺﾄﾌﾞｷO/Lﾌｧｲﾙ READ
002970     READ   JOJF        INVALID             GO  TO  SSD-030.
002980     MOVE   1           TO  NN.
002990 SSD-000.
003000     IF  JOJF-08(NN)    NOT =  11           GO  TO  SSD-010.
003010     IF  JOJF-09(NN)    NOT =  ZERO         GO  TO  SSD-020.
003020 SSD-010.
003030     IF     NN    =     10                  GO  TO  SSD-030.
003040     ADD    1           TO  NN.
003050     GO  TO  SSD-000.
003060 SSD-020.
003070     MOVE  JOJF-09(NN)  TO  KENSU.
003080     MOVE   SPACE       TO   F1-REC.
003090     MOVE   "ST"        TO   S-ID.
003100     MOVE   W-JCD       TO   S-JECD.
003110     MOVE   000         TO   S-AECD.
003120     MOVE   KENSU       TO   S-SKSU.
003130     MOVE   F1-REC      TO   SENDBUF.
003140     MOVE   64          TO   CSTL.
003150*
003160     SEND   SENDREC     FROM   SENDBUF  WITH  EGI.
003170     PERFORM  SEND-STS-RTN   THRU  SEND-STS-EX.
003180     GO  TO  SSD-040.
003190 SSD-030.
003200     MOVE   SPACE       TO   F1-REC.
003210     MOVE   "ST"        TO   S-ID.
003220     MOVE   W-JCD       TO   S-JECD.
003230     MOVE   000         TO   S-AECD.
003240     MOVE   ZERO        TO   S-SKSU.
003250     MOVE   F1-REC      TO   SENDBUF.
003260     MOVE   64          TO   CSTL.
003270*
003280     SEND   SENDREC     FROM   SENDBUF  WITH  EGI.
003290     PERFORM  SEND-STS-RTN   THRU  SEND-STS-EX.
003300 SSD-040.
003310     MOVE  0001     TO  JOJF-01.
003320*----O/Lﾌｧｲﾙ READ
003330     READ   JOJF        INVALID              GO  TO  SSD-050.
003340     MOVE  JOJF-07      TO   W-ACD.
003350*
003360     MOVE  JOJF-REC     TO   JOJF-RECW.
003370     MOVE  6            TO   JOJF-061.
003380     REWRITE  JOJF-REC  INVALID
003390                        MOVE  "JOJF"     TO  ERR-F
003400                        MOVE  JOJF-KEY   TO  ERR-K
003410                        MOVE  "R"        TO  ERR-M
003420                        PERFORM  ERR-RTN  THRU  ERR-EX
003430                        MOVE  255        TO  COMPLETION-CODE
003440                        MOVE  1          TO  ERR-SW.
003450     GO  TO  SSD-EX.
003460 SSD-050.
003470     MOVE    SPACE      TO    JOJF-REC.
003480     INITIALIZE               JOJF-REC.
003490     MOVE    0001       TO    JOJF-01.
003500     MOVE    6          TO    JOJF-061.
003510     MOVE    10         TO    JOJF-90.
003520     MOVE    JOJF-REC   TO    JOJF-RECW.
003530*
003540     WRITE  JOJF-REC  INVALID
003550                        MOVE  "JOJF"     TO  ERR-F
003560                        MOVE  JOJF-KEY   TO  ERR-K
003570                        MOVE  "W"        TO  ERR-M
003580                        PERFORM  ERR-RTN  THRU  ERR-EX
003590                        MOVE  255        TO  COMPLETION-CODE
003600                        MOVE  1          TO  ERR-SW.
003610 SSD-EX.
003620     EXIT.
003630******************************************************************
003640*    ＡＮＳ−ＲＴＮ　　（開始承認電文　受信）　                  *
003650******************************************************************
003660 ANS-RTN.
003670     MOVE   SPACE       TO   RECVBUF.
003680*
003690     RECEIVE  RECVREC   MESSAGE  INTO  RECVBUF.
003700     PERFORM  RECV-STS-RTN   THRU  RECV-STS-EX.
003710     IF  ERR-SW  =  1                  GO  TO  ANS-EX.            I.900411
003720*
003730     RECEIVE  RECVREC   MESSAGE  INTO  RECVEOT.
003740     PERFORM  RECV-STS-RTN   THRU  RECV-STS-EX.
003750     IF  ERR-SW  =  1                  GO  TO  ANS-EX.            I.900411
003760*
003770     IF       CREK   NOT   =   "3"
003780              GO     TO    ANS-RTN.
003790*
003800*
003810     MOVE   RECVBUF     TO   F1-REC.
003820*
003830     IF  W-JCD  NOT =  S-AECD          GO  TO  ANS-000.
003840     IF  S-KUB      =  SPACE
003850                        GO  TO  ANS-EX.
003860 ANS-000.
003870*
003880     MOVE  0001         TO  JOJF-01.
003890     READ  JOJF         INVALID
003900                        MOVE  "JOJF"     TO  ERR-F
003910                        MOVE  JOJF-KEY   TO  ERR-K
003920                        MOVE  "A"        TO  ERR-M
003930                        PERFORM  ERR-RTN  THRU  ERR-EX
003940                        MOVE   255       TO  COMPLETION-CODE
003950                        MOVE  1          TO  ERR-SW
003960                        GO  TO  ANS-EX.
003970     MOVE  ZERO         TO  JOJF-061.
003980     REWRITE  JOJF-REC  INVALID
003990                        MOVE  "JOJF"     TO  ERR-F
004000                        MOVE  JOJF-KEY   TO  ERR-K
004010                        MOVE  "R"        TO  ERR-M
004020                        PERFORM  ERR-RTN  THRU  ERR-EX
004030                        MOVE  255        TO  COMPLETION-CODE
004040                        MOVE  1          TO  ERR-SW
004050                        GO  TO  ANS-EX.
004060*
004070     MOVE  4            TO  MM.
004080     DISPLAY  DSP-MSG  WITH  BYPASS.
004090     MOVE  255          TO  COMPLETION-CODE.
004100     MOVE  1            TO  ERR-SW.
004110 ANS-EX.
004120     EXIT.
004130******************************************************************
004140*    ＭＳＧ−ＲＴＮ　　（データ送信）                            *
004150******************************************************************
004160 MSG-RTN.
004170     MOVE  2             TO  MM.
004180     DISPLAY  DSP-MSG1   WITH    BYPASS.
004190*
004200 MSG-010.
004210*----Ｏ／Ｌ送信ファイル　ＲＥＡＤ
004220     READ  KOLJF  AT  END    GO  TO  MSG-900.
004230 MSG-100.
004240*----出荷指図Ｔ　ＲＥＣＯＲＤ
004250*****MOVE  KOLJF11-KEYW TO  END-CODE.
004260     ADD   1            TO  SOUKEN.
004270 MSG-200.
004280     MOVE   KOLJF11-REC TO   SENDBUF.
004290     MOVE   256         TO   CSTL.
004300     SEND   SENDREC     FROM   SENDBUF  WITH  EMI.
004310     PERFORM  SEND-STS-RTN   THRU  SEND-STS-EX.
004320     IF  ERR-SW  =  1             GO  TO  MSG-EX.
004330     GO  TO   MSG-010.
004340 MSG-900.
004350     DISPLAY  DSP-AREA2 WITH  BYPASS.
004360     DISPLAY  DSP-KB01  WITH  BYPASS.
004370     DISPLAY  DSP-KB02  WITH  BYPASS.
004380 MSG-EX.
004390     EXIT.
004400******************************************************************
004410*    ＥＳＤ−ＲＴＮ　　（終了電文　送信）                        *
004420******************************************************************
004430 ESD-RTN.
004440     DISPLAY  DSP-KB02  WITH BYPASS.
004450*
004460     MOVE   ZERO        TO   ERR-SW.
004470     MOVE   SPACE       TO   F1-REC.
004480*
004490     MOVE   "ZZ"        TO   S-ID.
004500     MOVE   W-JCD       TO   S-JECD.
004510     MOVE   000         TO   S-AECD.
004520     MOVE   SOUKEN      TO   S-SKSU.
004530     MOVE   F1-REC      TO   SENDBUF.
004540     MOVE   64          TO   CSTL.
004550*
004560     SEND   SENDREC     FROM   SENDBUF  WITH  EGI.
004570     PERFORM  SEND-STS-RTN   THRU  SEND-STS-EX.
004580 ESD-EX.
004590     EXIT.
004600******************************************************************
004610*    ＡＮＥ−ＲＴＮ　　（終了確認電文　受信）　                  *
004620******************************************************************
004630 ANE-RTN.
004640     MOVE   SPACE       TO   RECVBUF.
004650
004660     RECEIVE  RECVREC   MESSAGE  INTO  RECVBUF.
004670     PERFORM  RECV-STS-RTN   THRU  RECV-STS-EX.
004680     IF  ERR-SW  =  1                  GO  TO  ANE-EX.            I.900411
004690*
004700     RECEIVE  RECVREC   MESSAGE  INTO  RECVEOT.
004710     PERFORM  RECV-STS-RTN   THRU  RECV-STS-EX.
004720     IF  ERR-SW  =  1                  GO  TO  ANE-EX.            I.900411
004730*
004740     IF       CREK   NOT   =   "3"
004750              GO     TO    ANE-RTN.
004760
004770
004780     MOVE   RECVBUF     TO   F1-REC.
004790     IF  S-KUB      =  SPACE           GO  TO  ANE-EX.
004800*----ｴﾗｰｼｮﾘ
004810     MOVE   SPACE        TO   W-EMGA.
004820     MOVE   MA02         TO   W-JKYO.
004830     MOVE   MC04         TO   W-EMGN.
004840     DISPLAY  DSP-CLER  WITH  BYPASS.
004850     DISPLAY  DSP-04  DSP-05  WITH  BYPASS.
004860     DISPLAY  DSP-CLER  WITH  BYPASS.
004870     MOVE   1            TO   ERR-SW.
004880     MOVE  255           TO   COMPLETION-CODE.
004890*
004900     MOVE  0001         TO  JOJF-01.
004910     READ  JOJF         INVALID
004920                        MOVE  "JOJF"     TO  ERR-F
004930                        MOVE  JOJF-KEY   TO  ERR-K
004940                        MOVE  "A"        TO  ERR-M
004950                        PERFORM  ERR-RTN  THRU  ERR-EX
004960                        GO  TO  ANE-EX.
004970     MOVE  ZERO         TO  JOJF-061.
004980     REWRITE  JOJF-REC  INVALID
004990                        MOVE  "JOJF"     TO  ERR-F
005000                        MOVE  JOJF-KEY   TO  ERR-K
005010                        MOVE  "R"        TO  ERR-M
005020                        PERFORM  ERR-RTN  THRU  ERR-EX.
005030 ANE-EX.
005040     EXIT.
005050******************************************************************
005060*    ＵＰＤ−ＲＴＮ　　（Ｏ／Ｌ状況Ｆ更新）　　　　　　　        *
005070******************************************************************
005080 UPD-RTN.
005090     MOVE  3            TO  MM.
005100     DISPLAY   DSP-MSG1  WITH  BYPASS.
005110*
005120     MOVE  0001         TO  JOJF-01.
005130     READ  JOJF         INVALID
005140                        MOVE  "JOJF"     TO  ERR-F
005150                        MOVE  JOJF-KEY   TO  ERR-K
005160                        MOVE  "A"        TO  ERR-M
005170                        MOVE  1          TO  ERR-SW
005180                        MOVE  255        TO  COMPLETION-CODE
005190                        PERFORM  ERR-RTN  THRU  ERR-EX
005200                        GO  TO  UPD-EX.
005210*
005220     MOVE  ZERO         TO  JOJF-02  JOJF-03  JOJF-04 JOJF-05
005230                            JOJF-061 JOJF-063 JOJF-07.
005240     MOVE  SPACE        TO  JOJF-062.
005250     INITIALIZE             JOJF-TBL.
005260*
005270     REWRITE  JOJF-REC  INVALID
005280                        MOVE  "JOJF"     TO  ERR-F
005290                        MOVE  JOJF-KEY   TO  ERR-K
005300                        MOVE  "R"        TO  ERR-M
005310                        MOVE  1          TO  ERR-SW
005320                        MOVE  255        TO  COMPLETION-CODE
005330                        PERFORM  ERR-RTN  THRU  ERR-EX.
005340 UPD-EX.
005350     EXIT.
005360******************************************************************
005370*    ＣＬＯ−ＲＴＮ　　（閉局）                                  *
005380******************************************************************
005390 CLO-RTN.
005400     PERFORM  CLO1-RTN       THRU  CLO1-EX.
005410     PERFORM  CLO2-RTN       THRU  CLO2-EX.
005420 CLO-EX.
005430     EXIT.
005440*----  (RECVREC)  ----*
005450 CLO1-RTN.
005460     DISABLE  INPUT   RECVREC.
005470     PERFORM  ENDI-STSR-RTN  THRU  ENDI-STSR-EX.
005480 CLO1-EX.
005490     EXIT.
005500*----  (SENDREC)  ----*
005510 CLO2-RTN.
005520     DISABLE  OUTPUT  SENDREC.
005530     PERFORM  ENDI-STSS-RTN  THRU  ENDI-STSS-EX.
005540 CLO2-EX.
005550     EXIT.
005560******************************************************************
005570*    ＥＮＤ−ＲＴＮ　　（終了処理）                              *
005580******************************************************************
005590 END-RTN.
005600     CLOSE    KOLJF  JOJF.
005610 END-EX.
005620     EXIT.
005630******************************************************************
005640*    ＳＥＮＤ−ＳＴＳ−ＲＴＮ　　（送信時エラー）                *
005650******************************************************************
005660 SEND-STS-RTN.
005670     MOVE   ZERO        TO   ERR-SW.
005680     MOVE   SPACE       TO   W-EMGA  W-EMGN.
005690     IF  CSSK  =  "00"            GO  TO  SEND-STS-EX.
005700     MOVE   1           TO   ERR-SW.
005710     MOVE   255         TO   COMPLETION-CODE.
005720     MOVE   CSSK        TO   W-EMGA1.
005730     MOVE   CSEK        TO   W-EMGA2.
005740     IF  CSSK  NOT =  "80"                  GO  TO  SEND-S01.
005750     IF  CSEK  NOT =  "A" AND "B" AND "C" AND "E" AND "G" AND "J"
005760         GO  TO   SEND-S01.
005770     IF  CSEK  =  "A"
005780         MOVE   SM-A         TO   W-EMGN.
005790     IF  CSEK  =  "B"
005800         MOVE   SM-B         TO   W-EMGN.
005810     IF  CSEK  =  "C"
005820         MOVE   SM-C         TO   W-EMGN.
005830     IF  CSEK  =  "E"
005840         MOVE   SM-E         TO   W-EMGN.
005850     IF  CSEK  =  "G"
005860         MOVE   SM-G         TO   W-EMGN.
005870     IF  CSEK  =  "J"
005880         MOVE   SM-J         TO   W-EMGN.
005890     GO  TO   SEND-S02.
005900 SEND-S01.
005910     MOVE   SM-ER       TO   W-EMGN.
005920 SEND-S02.
005930     DISPLAY  DSP-CLER  WITH  BYPASS.
005940     DISPLAY  DSP-05  DISP-BUZ-B  WITH  BYPASS.
005950     DISPLAY  DSP-CLER  WITH  BYPASS.
005960     PERFORM  ERR1-RTN  THRU  ERR1-EXT.
005970 SEND-STS-EX.
005980     EXIT.
005990******************************************************************
006000*    ＲＥＣＶ−ＳＴＳ−ＲＴＮ　　（受信時エラー）                *
006010******************************************************************
006020 RECV-STS-RTN.
006030     MOVE   ZERO        TO   ERR-SW.
006040     MOVE   SPACE       TO   W-EMGA  W-EMGN.
006050     IF  CRSK  =  "00"            GO  TO  RECV-STS-EX.
006060     MOVE   1           TO   ERR-SW.
006070     MOVE   255         TO   COMPLETION-CODE.
006080     MOVE   CRSK        TO   W-EMGA1.
006090     MOVE   CREK        TO   W-EMGA2.
006100     IF  CRSK  NOT =  "80"                  GO  TO  RECV-S01.
006110     IF  CREK  NOT =  "L" AND "R" AND "S" AND "T"
006120         GO  TO   RECV-S01.
006130     IF  CREK  =  "L"
006140         MOVE   RM-L         TO   W-EMGN.
006150     IF  CREK  =  "R"
006160         MOVE   RM-R         TO   W-EMGN.
006170     IF  CREK  =  "S"
006180         MOVE   RM-S         TO   W-EMGN.
006190     IF  CREK  =  "T"
006200         MOVE   RM-T         TO   W-EMGN.
006210     GO  TO   RECV-S02.
006220 RECV-S01.
006230     MOVE   RM-ER       TO   W-EMGN.
006240 RECV-S02.
006250     DISPLAY  DSP-CLER  WITH  BYPASS.
006260     DISPLAY  DSP-05  DISP-BUZ-B  WITH  BYPASS.
006270     DISPLAY  DSP-CLER  WITH  BYPASS.
006280     PERFORM  ERR1-RTN  THRU  ERR1-EXT.                           I.900411
006290 RECV-STS-EX.
006300     EXIT.
006310******************************************************************
006320*    ＥＮＤＩ−ＳＴＳ−ＲＴＮ　　（開・閉局時エラー）            *
006330******************************************************************
006340*----  ＲＥＣＥＩＶＥ  ----*
006350 ENDI-STSR-RTN.
006360     MOVE   ZERO        TO   ERR-SW.
006370     MOVE   SPACE       TO   W-EMGA  W-EMGN.
006380     IF  CRSK  =  "00"            GO  TO  ENDI-STSR-EX.
006390     MOVE   1           TO   ERR-SW.
006400     MOVE   255         TO   COMPLETION-CODE.
006410     MOVE   CRSK        TO   W-EMGA1.
006420     IF  CRSK  NOT =  "16"  AND  "20"       GO  TO  ENDI-R01.
006430     IF  CRSK  =  "16"
006440         MOVE   ENDI-16      TO   W-EMGN
006450     ELSE
006460         MOVE   ENDI-20      TO   W-EMGN.
006470     GO  TO   ENDI-R02.
006480 ENDI-R01.
006490     MOVE   ENDI-ER     TO   W-EMGN.
006500 ENDI-R02.
006510     DISPLAY  DSP-CLER  WITH  BYPASS.
006520     DISPLAY  DSP-05    DISP-BUZ-B  WITH  BYPASS.
006530     DISPLAY  DSP-CLER  WITH  BYPASS.
006540 ENDI-STSR-EX.
006550     EXIT.
006560*--------------------*
006570*----  ＳＥＮＤ  ----*
006580*--------------------*
006590 ENDI-STSS-RTN.
006600     MOVE   ZERO        TO   ERR-SW.
006610     MOVE   SPACE       TO   W-EMGA  W-EMGN.
006620     IF  CSSK  =  "00"            GO  TO  ENDI-STSS-EX.
006630     MOVE   1           TO   ERR-SW.
006640     MOVE   255         TO   COMPLETION-CODE.
006650     MOVE   CSSK        TO   W-EMGA1.
006660     IF  CSSK  NOT =  "16"  AND  "20"       GO  TO  ENDI-S01.
006670     IF  CSSK  =  "16"
006680         MOVE   ENDI-16      TO   W-EMGN
006690     ELSE
006700         MOVE   ENDI-20      TO   W-EMGN.
006710     GO  TO   ENDI-S02.
006720 ENDI-S01.
006730     MOVE   ENDI-ER     TO   W-EMGN.
006740 ENDI-S02.
006750     DISPLAY  DSP-CLER  WITH  BYPASS.
006760     DISPLAY  DSP-05  DISP-BUZ-B  WITH  BYPASS.
006770     DISPLAY  DSP-CLER  WITH  BYPASS.
006780 ENDI-STSS-EX.
006790     EXIT.
006800*=================================================================*
006810*    開局・閉局時の送信・受信エラー処理                           *
006820*=================================================================*
006830 ERR1-RTN.
006840*****MOVE  JOJF-RECW    TO  JOJF-REC.                             *D 93/03
006850*****MOVE  JOJF-90      TO  JOJF-01.                              *D 93/03
006860*****MOVE  W-MD         TO  JOJF-02.                              *D 93/03
006870*****MOVE  STR-JF       TO  JOJF-03.                              *D 93/03
006880*****ACCEPT  END-TIME   FROM  TIME.                               *D 93/03
006890*****MOVE  END-JF       TO  JOJF-04.                              *D 93/03
006900*****MOVE  8            TO  JOJF-061.                             *D 93/03
006910*****MOVE  CSEK         TO  JOJF-062.                             *D 93/03
006920*****MOVE  CSSK         TO  JOJF-063.                             *D 93/03
006930*****MOVE  ZERO         TO  JOJF-07.                              *D 93/03
006940*ERR1-010.                                                        *D 93/03
006950*****MOVE  SOUKEN       TO  JOJF-10(1).                           *D 93/03
006960*****MOVE  STR-CODE     TO  JOJF-11(1).                           *D 93/03
006970*****MOVE  END-CODE     TO  JOJF-12(1).                           *D 93/03
006980*****MOVE  ZERO         TO  JOJF-90.                              *D 93/03
006990*
007000*****WRITE  JOJF-REC    INVALID                                   *D 93/03
007010*****                   MOVE  "JOJF"     TO  ERR-F                *D 93/03
007020*****                   MOVE  JOJF-KEY   TO  ERR-K                *D 93/03
007030*****                   MOVE  "W"        TO  ERR-M                *D 93/03
007040*****                   PERFORM  ERR-RTN  THRU  ERR-EX            *D 93/03
007050*****                   GO  TO  ERR1-EXT.                         *D 93/03
007060*
007070     MOVE  0001         TO  JOJF-01.
007080     READ  JOJF         INVALID
007090                        MOVE  "JOJF"     TO  ERR-F
007100                        MOVE  JOJF-KEY   TO  ERR-K
007110                        MOVE  "A"        TO  ERR-M
007120                        PERFORM  ERR-RTN  THRU  ERR-EX
007130                        GO  TO  ERR1-EXT.
007140     MOVE  ZERO         TO  JOJF-061.
007150     REWRITE  JOJF-REC  INVALID
007160                        MOVE  "JOJF"     TO  ERR-F
007170                        MOVE  JOJF-KEY   TO  ERR-K
007180                        MOVE  "R"        TO  ERR-M
007190                        PERFORM  ERR-RTN  THRU  ERR-EX.
007200 ERR1-EXT.
007210     EXIT.
007220***
007230 COPY    LPERR.                                                   R.900411
007240***
007250 END
